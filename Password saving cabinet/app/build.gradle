apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "my_manage.password_box"
        minSdkVersion 28
        targetSdkVersion 29
        versionCode 9
        versionName '1.0.8'
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //添加如下配置 开启annotation processor
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

//用Gradle生成必须
    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    def listSubFile = {
        def resFolder = 'src/main/res/layouts'
        def files = file(resFolder).listFiles()
        def folders = []
        files.each {
            item1 -> folders.add(item1.absolutePath)
        }
        folders.add(file(resFolder).parentFile.absolutePath)

        return folders
    }

    sourceSets {
        main {
            res.srcDirs = listSubFile()
        }
    }

//    更改生成的文件的名称
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.all {
                def apkName = "我的管理${defaultConfig.versionName}"
                outputFileName = apkName + ".apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation('androidx.appcompat:appcompat:1.1.0',
            'androidx.constraintlayout:constraintlayout:1.1.3',
            'javax.annotation:javax.annotation-api:1.2',
            'com.android.support:design:28.0.0',                //tabLayout插件
            'org.projectlombok:lombok:1.16.8',                  //添加lombok依赖
            'org.jetbrains:annotations:15.0',                   //java注解
            'org.jasypt:jasypt:1.9.3',                          //加密包
            'com.alibaba:fastjson:1.1.55.android',
            'com.githang:android-lite-orm:1.10.0-beta',         //android-lite-orm数据库操作
            'com.github.fengwenyi:AppLib:0.0.11',               //图标引用
            'com.hanks:htextview-rainbow:0.1.6',                //第三方彩虹字TextView
            'com.github.niniloveyou:StateButton:v1.0.0',        //第三方圆角按键
            'com.baoyz.swipemenulistview:library:1.3.0',        //第三方listView，侧滑删除
            'net.sourceforge.jexcelapi:jxl:2.6.12',             //excel插件
            'com.orhanobut:dialogplus:1.11@aar',                //第三方对话框
            'com.classic.adapter:commonadapter:2.0',            //adapter集成,commonadapter
            'com.jakewharton:butterknife:10.0.0'                //butterknife
    )

    testImplementation 'junit:junit:4.12'
    androidTestImplementation ('androidx.test.ext:junit:1.1.1',
            'androidx.test.espresso:espresso-core:3.2.0')

    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

}
